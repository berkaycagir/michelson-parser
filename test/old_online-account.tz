parameter (or (unit %deposit) (mutez %withdraw));
storage (pair mutez address);
code { # (pair parameter storage) : []
       UNPAIR;
       # parameter : storage : []
       IF_LEFT
             { # %Deposit entrypoint
               # unit : pair mutez address : []
               DROP; 
               # pair mutez address : []
               # Update the storage with the new balance
               AMOUNT;
               # mutez : pair mutez address : []
               DIP{ UNPAIR; };
               # mutez : mutez : address : []
               ADD;
               # mutez : address : []
               PAIR;
               # pair mutez address : []
               NIL operation 
               # (list operation) : pair mutez address : []
             }
             { # %Withdraw entrypoint
               # mutez : pair mutez address : []
               SWAP;
               # pair mutez address : mutez : []
               DUP; DUP;
               # pair mutez address : pair mutez address : pair mutez address : mutez : []
               # Check whether the caller is the contract's owner
               CDR;
               # address : pair mutez address : pair mutez address : mutez : []
               SENDER;
               # address : address : pair mutez address : pair mutez address : mutez : []
               IFCMPEQ { 
                          # pair mutez address : pair mutez address : mutez : []
                          DIP { SWAP; DUP };
                          # pair mutez address :  mutez :  mutez : pair mutez address : []
                          # Check if the balance is sufficient 
                          CAR;
                          # mutez :  mutez :  mutez : pair mutez address : []
                          IFCMPLT { # mutez : pair mutez address : []
                                    PUSH string "The balance is not sufficient."; FAILWITH }
                                  { # mutez : pair mutez address : []
                                    DIP { DUP };
                                    # mutez : pair mutez address : pair mutez address : []
                                    DIP { UNPAIR }; DUP ;
                                    # mutez : mutez : mutez : address : pair mutez address : []
                                    DIP { SWAP; SUB ; PAIR };
                                    # mutez : pair mutez address : pair mutez address : []
                                    DIP { CDR } ;
                                    # mutez : address : pair mutez address : []
                                    SWAP; 
                                    # address : mutez : pair mutez address : []
                                    CONTRACT unit; 
                                    IF_NONE { PUSH string "The stored address is incorrect"; FAILWITH } 
                                            { # contract unit : mutez : pair mutez address : []
                                              SWAP;
                                              # mutez : contract unit : pair mutez address : []
                                              PUSH unit Unit; 
                                              # unit : mutez : contract unit : pair mutez address : []
                                              TRANSFER_TOKENS;
                                              # operation : pair mutez address : []
                                              NIL operation; SWAP; CONS
                                              # (list operation) : pair mutez address : []
                                            };
                                   };
                        } 
                        { PUSH string "Only owner can withdraw."; FAILWITH };
               };            
       PAIR };
